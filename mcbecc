#!/usr/bin/perl -n -w
use strict;
use Data::Dumper;

my $DEBUG = 0;

my %vars;
my @prog;
my @consts;
my $lastconst = 0; 
my $lastcond = 0; 

my ($main, $endmain) = (0,0);

my @conds = ();

#
# Analisis sintactico - busca cadenas e invoca a funciones que generan assembler
#
/^\/\// and next;
/int main()/ and setmain(1);

/^\s*int\s*([[:alpha:]]+\d*)\s*=\s*(-*\d*)\s*;\s*$/ and newinit($1, $2);	# int a = 1
/^\s*int\s*([[:alpha:]]+\d*)\s*;\s*$/ and newvar($1);			# int a

/^\s*([[:alpha:]]+\d*)\+\+ *; *$/ and asg3($1, $1, "+", 1);	# a++;
/^\s*([[:alpha:]]+\d*)-- *; *$/ and asg3($1, $1, "-", 1);	# a--;
/^\s*([[:alpha:]]+\d*) *= *([[:alpha:]]+\d*) *; *$/ and asg4($1, $2); 		# a = b;
/^\s*([[:alpha:]]+\d*) *= *([[:alpha:]]+\d*) *([\+-]) *(-*\d+) *; *$/ and asg3($1, $2, $3, $4);	# a = a + 1 o a = b + 1;
/^\s*([[:alpha:]]+\d*) *= *(-*\d*) *([\+-]) *([[:alpha:]]+\d*) *; *$/ and asg5($1, $2, $3, $4);	# a = 1 + a o a = 1 + b;
/^\s*([[:alpha:]]+\d*) *= *([[:alpha:]]+\d*) *([\+-]) *([[:alpha:]]+\d*) *; *$/ and asg6($1, $2, $3, $4);	# a = b + c

/^\s*([[:alpha:]]+\d*) *= *(-*\d+) *([\+-]) *(-*\d+) *; *$/ and asg2($1, $2, $3, $4);	# a = 1 + 2;
/^\s*([[:alpha:]]+\d*) *= *(-*\d+) *; *$/ and asg1($1, $2); 		# a = 1;

/^\s*in\(&([[:alpha:]]+\d*)\)\s*;\s*$/ and in($1);
/^\s*out\(([[:alpha:]]+\d*)\)\s*;\s*$/ and out($1);

/^\s*while\s*\(([[:alpha:]]+\d*)\s*\!=\s*(-*\d+)\)\s*{\s*$/ and while1begin($1,$2); # while(a != 2)
/^\s*while\s*\(([[:alpha:]]+\d*)\s*\!=\s*([[:alpha:]]+\d*)\)\s*{\s*$/ and while2begin($1,$2); # while(a != b)
/^\s*}/ and condend();

/^\s*if\s*\(([[:alpha:]]+\d*)\s*\!=\s*(-*\d+)\)\s*{\s*$/ and if1begin($1,$2); # if(a != 2)
/^\s*if\s*\(([[:alpha:]]+\d*)\s*\!=\s*([[:alpha:]]+\d*)\)\s*{\s*$/ and if2begin($1,$2); # if(a != b)

/^\s*printf\(\"([^)]*)\"\)\s*;\s*$/ and printstring($1);

sub setmain {
	my $v = shift;
	if($v == 0) {
		$endmain = 1;
	} else {
		$main = 1;
	}
}

sub fatal {
	my $msg = shift;
	print "error: $msg\n";
	exit;
}

sub myprint {
	my @msg = @_;
	print "@msg\n" if $DEBUG;
}

# genera rotulos con prefijo alfanumerico 
# y sufijo numerico de dos digitos unico y automaticamente creciente
sub mklbl {
	my $class = shift;

	return sprintf "%s%02d", $class, $lastconst++;
}

# la funcion printstring genera un segmento de programa que imprime un string 
# el segmento se automodifica incrementando la direccion argumento de la instruccion de OUT
# esta version de la funcion genera un programa que guarda la direccion original del string
# y asi puede ser llamada mas de una vez - al costo de dos bytes extra   
# printstring2 genera un programa mas corto pero no puede ser llamado mas de una vez por cada string
sub printstring {
	my $str = shift;

	my @chars = split "", $str;
	my $lbl = mklbl("LBL"); # incremento
	my $inc = mklbl("INC"); # incremento
	my $begin = mklbl("STR"); # puntero
	my $end = mklbl("END");
	my $ren = mklbl("REN"); # reentrancia
	push @consts, "$inc:\t1";
	push @consts, "$ren:\tLD $begin";
	for my $i (0 .. $#chars) {
		push @consts, (sprintf "%s\t%d", ($i ? "" : "$begin:"), ord($chars[$i]));
	}
	push @consts, "\t0"; # string null
	push @prog, (
		"$lbl:\tLD $begin", 
		"\tJZ $end",
		"\tST OUT",
		"\tLD $lbl",
		"\tADD $inc",
		"\tST $lbl",
		"\tJMP $lbl",
		"$end:\tLD $ren",
		"\tST $lbl"
	);
}

# esta version de printstring genera un segmento de programa 
# que se automodifica y no puede ser llamado mas de una vez 
sub printstring2 { # 
	my $str = shift;

	my @chars = split "", $str;
	my $lbl = mklbl("LBL"); # incremento
	my $inc = mklbl("INC"); # incremento
	my $begin = mklbl("STR"); # puntero
	my $end = mklbl("END");
	push @consts, "$inc:\t1";
	for my $i (0 .. $#chars) {
		push @consts, (sprintf "%s\t%d", ($i ? "" : "$begin:"), ord($chars[$i]));
	}
	push @consts, "\t0"; # string null
	push @prog, (
		"$lbl:\tLD $begin", 
		"\tJZ $end",
		"\tST OUT",
		"\tLD $lbl",
		"\tADD $inc",
		"\tST $lbl",
		"\tJMP $lbl",
		"$end:\tNOP"
	);
}

sub if1begin {# if(a != 2)
	my ($k, $v) = @_;
	
	testvar($k);
	
	my $dato = mklbl("DATO");
	push @consts, "$dato:\t$v";

	my $conde = "COND".$lastcond;
	push @prog, (
	(sprintf "\tLD %s", uc($k)),
		 "\tSUB $dato",
		" \tJZ $conde"
	);
	push @conds, "$conde:\tNOP";
	$lastcond++;
}

sub if2begin {# if(a != b)
	my ($k1, $k2) = @_;
	
	testvar($k1);
	testvar($k2);
	
	my $conde = "COND".$lastcond;
	push @prog, (
		(sprintf "\tLD %s", uc($k1)),
		(sprintf "\tSUB %s", uc($k2)),
		         "\tJZ $conde"
	);
	push @conds, "$conde:\tNOP";
	$lastcond++;
}

sub while1begin {# while(a != 2)
	my ($k, $v) = @_;
	
	testvar($k);
	
	my $dato = mklbl("DATO");
	push @consts, "$dato:\t$v";

	my $loopb = "LAZO".$lastcond;
	my $loope = "FIN".$lastcond;
	push @prog, (
		(sprintf "%s:\tLD %s", $loopb, uc($k)),
		"\tSUB $dato",
		"\tJZ $loope"
	);
	push @conds, "\tJMP $loopb\n$loope:\tNOP";
	$lastcond++;
}

sub while2begin {# while(a != b)
	my ($k1, $k2) = @_;
	
	testvar($k1);
	testvar($k2);
	
	my $loopb = "LAZO".$lastcond;
	my $loope = "FIN".$lastcond;
	push @prog, (
		(sprintf "%s:\tLD %s", $loopb, uc($k1)),
		(sprintf "\tSUB %s", uc($k2)),
		"\tJZ $loope"
	);
	push @conds, "\tJMP $loopb\n$loope:\tNOP";
	$lastcond++;
}

sub condend {
	my $ends = pop @conds;
	if(defined $ends) {
		push @prog, $ends;
	} else {
		setmain(0); # } cierra main
	}
}

sub in {
	my $k = shift;

	testvar($k);
	push @prog, (
		"\tLD IN",
		(sprintf "\tST %s", uc($k))
	);
}

sub out {
	my $k = shift;

	testvar($k);
	push @prog, (
		(sprintf "\tLD %s", uc($k)),
		"\tST OUT"
	);
}

	
sub asg4 { # a = b
	my ($k1, $k2) = @_;

	testvar($k1);
	testvar($k2);
	
	if($k1 eq $k2) { # a = a 
		return; # optimizing compiler!
	} else { # a = b 
		push @prog, (
			(sprintf "\tLD %s", uc($k2)),
			(sprintf "\tST %s",uc($k1))

		)
	}
}

sub asg6 { # a = b + c
	my ($k1, $k2, $s, $k3) = @_;

	testvar($k1);
	testvar($k2);
	testvar($k3);
	
	push @prog, (
		(sprintf "\tLD %s", uc($k2)),
		(sprintf "\t%s %s", ($s eq "+") ? "ADD" : "SUB", uc($k3)),
		(sprintf "\tST %s",uc($k1))
	);
}

sub asg5 { # a = 1 + a o a = 1 + b
	my ($k1, $v, $s, $k2) = @_;

	testvar($k1);
	testvar($k2);
	
	my $dato = mklbl("DATO");
	push @consts, "$dato:\t$v";

	push @prog, (
		(sprintf "\tLD $dato"),
		(sprintf "\t%s %s", ($s eq "+") ? "ADD" : "SUB", uc($k2)),
		(sprintf "\tST %s",uc($k1))
	);
}

sub asg3 { # a = a + 1 o a = b + 1
	my ($k1, $k2, $s, $v) = @_;

	testvar($k1);
	testvar($k2);
	
	my $dato = mklbl("DATO");
	push @consts, "$dato:\t$v";

	push @prog, (
		(sprintf "\tLD %s", uc($k2)),
		(($s eq "+") ? "\tADD $dato" : "\tSUB $dato"),
		(sprintf "\tST %s",uc($k1))
	)
}

sub ex_asg3 { # a = a + 1 o a = b + 1
	my ($k1, $k2, $s, $v) = @_;

	testvar($k1);
	testvar($k2);
	
	my $dato = mklbl("DATO");
	push @consts, "$dato:\t$v";

	if($k1 eq $k2) { # a = a + 1
		push @prog, (
			(sprintf "\tLD %s", uc($k1)),
			(($s eq "+") ? "\tADD $dato" : "\tSUB $dato"),
			(sprintf "\tST %s",uc($k1))
		);
	} else { # a = b + 1
		push @prog, (
			(sprintf "\tLD %s", uc($k2)),
			(($s eq "+") ? "\tADD $dato" : "\tSUB $dato"),
			(sprintf "\tST %s",uc($k1))

		)
	}
}

sub asg1 { # a = 1
	my ($k, $v) = @_;

	testvar($k);
	
	my $dato = mklbl("DATO");
	push @consts, "$dato:\t$v";
	push @prog, (
		"\tLD $dato",
		sprintf "\tST %s",uc($k)
	);
}

sub asg2 { # a = 1 + 2
	my ($k, $v1, $s, $v2) = @_;

	myprint "ENTRANDO EN ASG2 s=$s\n";

	testvar($k);
	
	my $dato1 = mklbl("DATO");
	my $dato2 = mklbl("DATO");

	push @consts, "$dato1:\t$v1";
	push @consts, "$dato2:\t$v2";
	my $seq1 = sprintf "\t%s $dato2", ($s eq "+") ? "ADD" : "SUB";
	my $seq2 = sprintf "\tST %s", uc($k);
	push @prog, (
		"\tLD $dato1",
		$seq1, 
		$seq2
	);
}
	

sub testvar {
	my $k = shift;
	
	if(! defined $vars{$k}) {
		fatal("Variable $k no declarada");
	} else { 
		myprint("Encontrado $k con ".$vars{$k});
	}
}

sub newvar {
	my $k = shift;

	if(defined $vars{$k}) {
		fatal("Variable $k redeclarada");
	}
	$vars{$k} = 0;
}

sub newinit {
	my ($k, $v) = @_;

	if(defined $vars{$k}) {
		fatal("Variable $k redeclarada");
	}
	$vars{$k} = $v;
}

sub outprog {
	push @prog, ("\tHLT");
	foreach my $i (@prog) {
		print "$i\n";
	}
}

sub outvars {
	while(my ($k,$v) = each %vars) {
		printf "%s:\t%s\n", uc($k), $v;
	}
}

sub outconsts {
	foreach my $i (@consts) {
		print "$i\n";
	}
}


END {
	$DEBUG=0;
	myprint(Dumper(%vars));
	myprint(Dumper(@prog));
	myprint(Dumper(@consts));
	myprint("-----------");
	if($main == 0) {
		fatal("error: Falta main()");
	}
	if($endmain == 0) {
		fatal("error: Falta cerrar main()");
	}
	outprog;
	outvars;
	outconsts;
	
}		
